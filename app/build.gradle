apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'

final travisKeystorePassword = System.getenv('KEYSTORE_PASSWORD') ?: ''
final travisKeyAlias = System.getenv('KEY_ALIAS') ?: ''
final travisKeyPassword = System.getenv('KEY_PASSWORD') ?: ''
final travisVersionCode = System.getenv('TRAVIS_BUILD_NUMBER')?.toInteger() ?: 0
final travisVersionName = "0.3.0.${System.getenv('TRAVIS_BUILD_NUMBER') ?: 0}"

android {
    compileSdkVersion 27
    buildToolsVersion '27.0.3'
    defaultConfig {
        applicationId "com.github.wrdlbrnft.betterbarcodes.example.app"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode travisVersionCode
        versionName travisVersionName
    }

    dataBinding {
        enabled true
    }

    signingConfigs {
        release {
            storeFile rootProject.file('release.jks')
            storePassword travisKeystorePassword
            keyAlias travisKeyAlias
            keyPassword travisKeyPassword
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:27.0.2'
    compile 'com.android.support:design:27.0.2'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.github.wrdlbrnft:better-barcodes:0.3.0.46'
}

play {
    track = 'beta'
    jsonFile = rootProject.file('keys.json')
}
